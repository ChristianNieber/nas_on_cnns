<features> ::= <convolution> | <pooling>
<convolution> ::= layer:conv [num-filters,int,2,256] [filter-shape,int,2,5] [stride,int,1,3] <activation-function> <padding> <bias> <batch-norm>
<batch-norm> ::= batch-norm:True | batch-norm:False
<pooling> ::= <pool-type> [kernel-size,int,2,5] [stride,int,1,3] <padding>
<pool-type> ::= layer:pool-avg | layer:pool-max
<padding> ::= padding:same | padding:valid

<classification> ::= layer:fc <activation-function> [num-units,int,64,2048] <bias> <batch-norm>
<activation-function> ::= act:linear | act:relu | act:elu | act:sigmoid
<bias> ::= bias:True | bias:False
<output> ::= layer:output num-units:10 bias:True

<learning> ::= <gradient-descent> [batch_size,int,50,2048] | <rmsprop> [batch_size,int,50,2048] | <adam> [batch_size,int,50,2048]
<gradient-descent> ::= learning:gradient-descent [lr,float,0.0001,0.1] [momentum,float,0.68,0.99] <nesterov>
<nesterov> ::= nesterov:True | nesterov:False
<adam> ::= learning:adam [lr,float,0.0001,0.1] [beta1,float,0.5,1] [beta2,float,0.5,1]
<rmsprop> ::= learning:rmsprop [lr,float,0.0001,0.1] [rho,float,0.5,1]